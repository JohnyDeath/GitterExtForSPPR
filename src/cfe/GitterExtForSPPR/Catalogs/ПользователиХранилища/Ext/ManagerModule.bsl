
Функция ПолучитьПоНаименованию(Наименование, Хранилище) Экспорт
	
	Пользователь = НайтиПоНаименованию(Наименование, Истина,, Хранилище);
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		ВызватьИсключение "Не удалось получить пользователя по наименованию " + Наименование;
	КонецЕсли;
	
	Возврат Пользователь;
	
КонецФункции

Функция ПараметрыПользователя( Знач пИмяИлиСсылка, Знач пХранилище = Неопределено) Экспорт
	
	Если ТипЗнч( пИмяИлиСсылка ) = Тип( "Строка" ) Тогда
		
		Запрос       = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВЫБОР
			|		КОГДА ПользователиХранилища.УчетнаяЗапись = ЗНАЧЕНИЕ(Справочник._УчетныеЗаписи.ПустаяСсылка)
			|			ТОГДА ПользователиХранилища.Email
			|		ИНАЧЕ ПользователиХранилища.УчетнаяЗапись.Email
			|	КОНЕЦ КАК Email,
			|	ВЫБОР
			|		КОГДА ПользователиХранилища.УчетнаяЗапись = ЗНАЧЕНИЕ(Справочник._УчетныеЗаписи.ПустаяСсылка)
			|			ТОГДА ПользователиХранилища.ИмяПользователя
			|		ИНАЧЕ ПользователиХранилища.УчетнаяЗапись.Наименование
			|	КОНЕЦ КАК Имя,
			|	ПользователиХранилища.УчетнаяЗапись,
			|	ПользователиХранилища.Ссылка
			|ИЗ
			|	Справочник.ПользователиХранилища КАК ПользователиХранилища
			|ГДЕ
			|	ПользователиХранилища.Наименование = &Наименование
			|	И ПользователиХранилища.Владелец = &Владелец";
		Запрос.УстановитьПараметр( "Наименование", пИмяИлиСсылка );
		Запрос.УстановитьПараметр( "Владелец", пХранилище );
		
	Иначе
		
		Запрос       = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			|	ВЫБОР
			|		КОГДА ПользователиХранилища.УчетнаяЗапись = ЗНАЧЕНИЕ(Справочник._УчетныеЗаписи.ПустаяСсылка)
			|			ТОГДА ПользователиХранилища.Email
			|		ИНАЧЕ ПользователиХранилища.УчетнаяЗапись.Email
			|	КОНЕЦ КАК Email,
			|	ВЫБОР
			|		КОГДА ПользователиХранилища.УчетнаяЗапись = ЗНАЧЕНИЕ(Справочник._УчетныеЗаписи.ПустаяСсылка)
			|			ТОГДА ПользователиХранилища.ИмяПользователя
			|		ИНАЧЕ ПользователиХранилища.УчетнаяЗапись.Наименование
			|	КОНЕЦ КАК Имя,
			|	ПользователиХранилища.УчетнаяЗапись,
			|	ПользователиХранилища.Ссылка
			|ИЗ
			|	Справочник.ПользователиХранилища КАК ПользователиХранилища
			|ГДЕ
			|	ПользователиХранилища.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр( "Ссылка", пИмяИлиСсылка );
		
	КонецЕсли;
	
	выборка = Запрос.Выполнить().Выбрать();
	
	структ = Новый Структура;
	структ.Вставить( "Ссылка" );
	структ.Вставить( "Email" );
	структ.Вставить( "Имя" );
	структ.Вставить( "УчетнаяЗапись" );
	
	Если выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств( структ, выборка );
		
	КонецЕсли;
	
	Возврат структ;
	
КонецФункции 
